/** Modify columns
*/
Class ModifyCol_vgui extends Modify_vgui {

	_hwnd	:= {"gui":"", "list_view":""}

	/** hwnd
	*/
	hwnd($gui, $list_view){
		;MsgBox,262144,gui, %$gui%,3
		this._hwnd.gui	:= $gui
		this._hwnd.list_view	:= $list_view
		return this
	}

	/*-----------------------------------------
		MODIFY METHODS
	-----------------------------------------
	*/
	/** setColumns
	*/
	setColumns($count){
		loop, % $count
			this._options.push("")
	}
	/**  modifyCol HELP: https://autohotkey.com/docs/commands/ListView.htm#bifCol
		A) modifyCol()	; Run modify all columns by this._options
		B) modifyCol("Auto")	; Modify all columns by value
		C) modifyCol(1, "Auto")	; Modify columns 1 with value
		D) modifyCol([1,3], "Auto")	; Modify columns 1 & 3 with value
		E) modifyCol({1:64, 3:"Auto"})	; modify exact columns with their values
	*/
	modifyCol($param1:="", $param2:=""){
		;Dump(this, "this.", 1)
		$isObject := isObject($param1)

		if( !$isObject && !$param2)                        ; example B
			this._optionToAll($param1)
		 else if( $param1 is number && $param2)        ; example C
			this._addOption($param1, $param2)
		 else if( $isObject && $param2!="")        ; example D
			this._addOptionToColumns($param1, $param2)
		else if( $isObject && $param2=="" )                   ; example E
			this._addOptionsObject($param1)

		if(this._hwnd.list_view) ; If list view already exists
			For $column, $options in % this._options
				this._applyModifyCol($column, $options)

		this.fitColumns()
	}
	/** fit Last Columns to width of list view
	*/
	fitColumns(){
		this._applyModifyCol(this._options.MaxIndex(), "AutoHdr")
	}
	/*-----------------------------------------
		PRIVATE
	-----------------------------------------
	*/
	/** _applyModifyCol
	*/
	_applyModifyCol($column, $options){
		this._activateListView()
		;Dump($options, $column, 1)
		if($options)
			LV_ModifyCol($column, $options)
	}



}
