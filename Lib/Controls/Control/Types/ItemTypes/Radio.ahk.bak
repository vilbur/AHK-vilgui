/** Class Radio_vgui
*/
Class Radio_vgui extends Control_vgui{

	/** Add to GUI
	*/
	add($name:=""){
		;MsgBox,262144,, add,2
		this.name($name)
		this.preAdd()
		return % this.Controls()
	}
	/** Proceed methods necessary BEFORE adding of object to GUI
	*/
	preAdd(){
		this.addRadiobuttons()
		this._resetControlInParent()
	}
	/** Get configured Control object which is able passed to Controls.add()
	*/
	get(){
		this.preAdd()
		return this
	}

	/** addRadiobuttons
	*/
	addRadiobuttons(){
		$options := this.options()
;Dump($options, "options", 1)

		For $key, $item in this._items
			if($item)
				this.name( this._name "." this._getButtonName($key, $item) )
					.value($item)
					.options( $options (A_Index==1 ? " Group ":"") this._isChecked($key) )
					.Controls()
					.add(this.clone())
	}
	/** Set checked radio button key or number
		@param int|string $key of checked item
	*/
	checked($key){
		this.checked_key := $key
		return this
	}
	/** Is radio button checked ?
		If this.checked_key == key
		Or if next item is empty because of items was entered as string E.G: "Item A||Item B|Item C"

		@param int|string $key of checked radio button
	*/
	_isChecked($key){
		return % " checked" (this.checked_key == $key || ( $key<this._items.MaxIndex() && this._items[$key+1] == "" ))
	}
	/** Get name of radio button
		If items are string or array, name is value without whitepsace	E.G: IF items= "Item A|Item B" OR ["Item A", "Item B"]	THEN keys are "ItemA|ItemB"
		If items are object the return key	E.G: IF items= {"key1":"Item A", "key2":"Item A"}	THEN keys are "key1|key2"
		@return string
	*/
	_getButtonName($key, $value){
		return % $key is number ? RegExReplace( $value, "\s+", "" ) : $key
	}
}
